plugins {
	id 'fabric-loom' version '0.9-SNAPSHOT'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "${project.minecraft_version}-${project.archives_base_name}"
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "fabricmc"
		url = "https://maven.fabricmc.net"
	}
	maven {
		name = "CottonMC"
		url = "https://server.bbkr.space/artifactory/libs-release"
	}
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/"}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}") {
		exclude module: "fabric-api"
	}
	modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	modImplementation include("io.github.cottonmc:LibGui:${project.libgui_version}")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 16
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

curseforge {
	apiKey = "${curseforge_apikey}"
	project {
		id = '304889'
		changelog = file('changelog.txt') // A file can also be set using: changelog = file('changelog.txt')
		releaseType = 'release'
		addGameVersion "${minecraft_version}"
		//addGameVersion 'Fabric'
		mainArtifact(remapJar) {
			displayName = "$archivesBaseName-$mod_version-Fabric"
			relations {
				embeddedLibrary "libgui"
				requiredDependency "fabric-api"
			}
		}
	}
	options {
		debug = true
		forgeGradleIntegration = false
	}
}

afterEvaluate {
	tasks.curseforge304889.dependsOn remapJar
}